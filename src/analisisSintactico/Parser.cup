package analisisSintactico;

// External dependencies
import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java.util.ArrayList;

// Module dependencies
import intermedio.Param;
import Checkers.Tipo;
import Checkers.TipoOperador;
import Checkers.TypeCheck;
import Checkers.OverflowCheck;
import Checkers.TipoObject;
import Errores.ErrorHandler;
import Errores.ErrorSemantico;
import Errores.ErrorSintactico;
import Procesador.GlobalVariables;
import Procesador.EntornoClase;
import Procesador.EntornoFuncion;
import Procesador.Declaracion;
import Procesador.DeclaracionFuncion;
import Procesador.DeclaracionConstante;
import Procesador.DeclaracionClase;
import Procesador.DeclaracionArray;
import Procesador.Identificador;
import SimbolosNoTerminales.*;
import intermedio.I3DUtils;
import intermedio.OperacionTresDirecciones;
import intermedio.InstruccionTresDirecciones;


parser code {:


  /** Trata errores de tipo sintáctico **/
	@Override
	public void report_error(String message, Object info) {
		if(info != null) {
			ComplexSymbol simboloEncontrado = (ComplexSymbol) info;
			ErrorHandler.reportaError(new ErrorSintactico(simboloEncontrado));
		}else {
			ErrorHandler.reportaError(""+message);
		}
	}
	
	/** Para controlar que el parser no imprima texto de los errores por pantalla **/
	@Override
	public void syntax_error(Symbol token) {
		report_error("Syntax error", token);
	}
	@Override
	public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
		report_fatal_error("\nCould not recover and keep parsing\n", null);
	}

 :}

terminal    String			TCLASS, TVAR, TFINAL, TRETURN, TVOID, TIF, TWHILE, TELSE, LLAVEIZQ, LLAVEDER, FUNCTION;
terminal    String			IGUAL, PUNTOCOMA, COMA, PARENIZQ, PARENDER, CORCHIZQ, CORCHDER;
terminal	String			COMPARADOR, OPLOGICO, VBOOLEANO, OPSUMA, OPPROD;
terminal	String			VSTRING;
terminal	String			VNUMERO;
terminal	String			VID;

non terminal	SimboloBucle		bucle;
non terminal	SimboloCondicional	condicional;
non terminal	SimboloCondElse		condelse;
non terminal	SimboloExpresion	expresion;
non terminal	SimboloPrograma		programa;
non terminal	SimboloClase		clase;
non terminal	SimboloClase		clase_primera_parte;
non terminal	DeclaracionClase		clase_firma;
non terminal	SimboloCuerpo		cuerpo;
non terminal	SimboloAsignacion	asignacion;
non terminal  	SimboloFactor		factor;
non terminal	SimboloOperacion	Operacion;
non terminal	SimboloOperacion	Operacion1;
non terminal	SimboloOperacion	Operacion2;
non terminal	SimboloOperacion	Operacion3;
non terminal  	SimboloFuncionInvk	funcionInvk;
non terminal	SimboloFuncionDecl	funcionDecl;
non terminal	SimboloFuncionDecl	funchead;
non terminal	SimboloFuncionDecl	funcname;
non terminal	SimboloArgs			args;
non terminal	SimboloArgDecl		argDecl;
non terminal	SimboloArgs			nextArg;
non terminal	SimboloContenido	contenido;
non terminal	SimboloParams		params;
non terminal	SimboloParams		nextParam;
non terminal  SimboloCondicionBucle cond_bucle;
non terminal  SimboloEtiqueta M1;
non terminal  SimboloCondicionalBloquePrincipal cond_main_block;
non terminal  SimboloClausulaCondicion clausula_cond;
non terminal	SimboloArray 		arrayAsig;
non terminal	SimboloArray 		arrayDef;
non terminal	SimboloInicializacion inicializacion;
non terminal 	SimboloDeclaracion	declaracion;
non terminal 	SimboloDeclaraciones	declaraciones;

precedence left OPSUMA;
precedence left OPPROD;
precedence left OPLOGICO, COMPARADOR;

start with programa;

programa ::=  												{: GlobalVariables.entraBloqueClase(null);
															:}
			clase:c
															{: GlobalVariables.saleBloqueClase();
																RESULT = new SimboloPrograma(c);
															:}
			;
			
clase	::=	clase_primera_parte:cpp cuerpo:c	LLAVEDER
			{: 
					SimboloClase simbolo = cpp;
					simbolo.setCuerpo(c);
					
					// Implementamos una pseuda llamada a la inizialicacion de variables de la clase
					I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, simbolo.getEtiquetaPreInicializacion());
					I3DUtils.crea(OperacionTresDirecciones.CLASE, simbolo.getDeclaracionClase());
					I3DUtils.crea(OperacionTresDirecciones.GOTO, simbolo.getEtiquetaDeclaraciones());
					I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, simbolo.getEtiquetaPostInicializacion());
					
					try {
							// Implementamos la llamada al main 
							DeclaracionFuncion decl = GlobalVariables.getMainFunction();
							I3DUtils.crea(OperacionTresDirecciones.ENTRY_POINT, decl);
					} catch(ErrorSemantico e) {
							ErrorHandler.reportaError(e);
					}
					GlobalVariables.saleBloqueClase();

					RESULT = simbolo;
			:}
			;

clase_primera_parte ::= clase_firma:cf declaraciones:decls
			{:
					I3DUtils.crea(OperacionTresDirecciones.GOTO, cf.getEtiquetaPostInicializacion());
					GlobalVariables.declaraBuiltInFunctions();
					RESULT = new SimboloClase(cf, decls);
			:};

clase_firma ::= TCLASS VID:i LLAVEIZQ
			{:
					DeclaracionClase decl = null;
					try {
							decl = GlobalVariables.asignaClaseID(i);
							I3DUtils.crea(OperacionTresDirecciones.GOTO, decl.getEtiquetaPreInicializacion());
							I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, decl.getEtiquetaDeclaraciones());
							GlobalVariables.entraBloqueClase(decl);
					} catch(ErrorSemantico e) {
							ErrorHandler.reportaError(e);
					}

					RESULT = decl;
			:};

cuerpo ::= cuerpo:c funcionDecl:f 							{: RESULT = new SimboloCuerpo(c,f); :}
			|												{: RESULT = null; :}
			;

declaraciones ::= declaracion:d declaraciones:decls
			{:
				RESULT = new SimboloDeclaraciones(d, decls);
			:}
			|
			{:
				RESULT = null;
			:}
			;

inicializacion ::= IGUAL Operacion:o PUNTOCOMA				
			{: 
					RESULT = new SimboloInicializacion(o);
			:}
			| PUNTOCOMA										
			{: 
					RESULT = new SimboloInicializacion(); 
			:}
			;

declaracion ::= TVAR:t VID:i arrayAsig:a inicializacion:init 
			{:  
				Boolean esArray = (a != null);
				Declaracion declResultado = null;
				try{
					if(esArray) {
						// Declaracion de un array
						declResultado = GlobalVariables.asignaArray(i,t,a);
						if(init.getSimboloOperacion()!=null) {
							TypeCheck.lanzaErrorArrayNoInicializable();
						}						
						I3DUtils.crea(OperacionTresDirecciones.DECLARAR_INDIRECCION, declResultado);
					} else {
						// Declaracion simple
						declResultado = GlobalVariables.asignaID(i,t);
						if(init.getSimboloOperacion()!=null) {
							TypeCheck.typesMatchAsignacion(i,Tipo.getTipo(t), init.getSimboloOperacion().getTipoSubyacente());
							I3DUtils.crea(OperacionTresDirecciones.COPIA, init.getDeclaracionResultado(), declResultado);						
						}
					}
						
				}catch(ErrorSemantico e){
					ErrorHandler.reportaError(e);
				}
				// Ojo! Aqui no es necesario propagar la declaracion ya no hay nadie que pueda usarlo en reducciones
				// posteriores. Así que declResultado muere aqui.
				RESULT = new SimboloDeclaracion(i,Tipo.getTipoSafe(t),esArray,init);
			:}
															
			| TFINAL TVAR:t VID:i inicializacion:init	
			{: 
				Declaracion decl = null;
				try{
          decl = GlobalVariables.asignaIDConstante(i,t, init.getDeclaracionResultado());

					if(init.getSimboloOperacion()!=null)
						TypeCheck.typesMatchAsignacion(i,Tipo.getTipo(t), init.getSimboloOperacion().getTipoSubyacente());
					else
						TypeCheck.lanzaErrorConstanteNoInicializada(i, t);

        	I3DUtils.crea(OperacionTresDirecciones.COPIA, init.getDeclaracionResultado(), decl);
				}catch(ErrorSemantico e){
					ErrorHandler.reportaError(e);
				}

				RESULT = new SimboloDeclaracion(i, Tipo.getTipoSafe(t), false, init);
			:}
			;
															
asignacion ::= VID:i arrayAsig:a IGUAL Operacion:o PUNTOCOMA 
			{: 
				try{
					if(a != null){
						DeclaracionArray decl = GlobalVariables.compruebaIDArray(i);
						TypeCheck.typesMatchAsignacionArray(i, o.getTipoSubyacente());
						// Y aqui va otro hack! :D
						// Cuando tenemos un elemento que es un array, para poder gestionar la indireccion
						// de forma adecuada en el c3@ tenemos que crear una constante intermedia que mantenga
						// la información del indice :+1:
						DeclaracionConstante indice = GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Integer), a.getNumero());
						// Para copiar un elemento dentro de un array tenemos que asignar un valor
						// en una posicion concreta.
						// Dicha posicion es: tamTipo * idx. Pero como las instrucciones operan sobre variables
						// tenemos que inventarnos
        		I3DUtils.crea(OperacionTresDirecciones.GUARDAR_INDIRECCION, o.getDeclaracionResultado(), decl, indice);						
					} else {
						Declaracion decl = GlobalVariables.compruebaID(i);
						GlobalVariables.compruebaAsignacionPermitida(i);
						TypeCheck.typesMatchAsignacion(GlobalVariables.entornoActual().fullGet(i).getId().getId(), GlobalVariables.entornoActual().fullGet(i).getTipo(), o.getTipoSubyacente());						
					
        		I3DUtils.crea(OperacionTresDirecciones.COPIA, o.getDeclaracionResultado(), decl);
					}
				}catch(ErrorSemantico e){
					ErrorHandler.reportaError(e);
				}
				// Ojo! Aqui no es necesario propagar la declaracion ya no hay nadie que pueda usarlo en reducciones
				// posteriores. Así que declResultado muere aqui
				RESULT = new SimboloAsignacion(i,o);
			:}
			;
			
arrayAsig  ::= CORCHIZQ VNUMERO:n CORCHDER					{: RESULT = new SimboloArray(n); :}
		 	|												{: RESULT = null; :}
		 	;
arrayDef   ::= CORCHIZQ CORCHDER							{: RESULT = new SimboloArray(); :}
		 	|												{: RESULT = null; :}
			;

funcionDecl ::= funchead:fh LLAVEIZQ contenido:c LLAVEDER
  {:  
      GlobalVariables.saleBloqueFuncion(false);
			// Ojo! Este return garantiza que las funciones
			// que no devuelven nada ( return void ) vuelven
			I3DUtils.crea(OperacionTresDirecciones.RETORNO, fh.getDeclaracion());
      // Watch out! fh holds a SimboloFuncionDecl instance
      fh.setContenido(c);
      RESULT = fh; 
  :}; 

funchead ::= funcname:fn PARENIZQ args:a PARENDER 
  {:  
      // Propagate the args upwards
      fn.setArgs(a);
			fn.finalizar();
      RESULT = fn; 
  :}; 

funcname ::= FUNCTION TVAR:t VID:i 
  {:  
      DeclaracionFuncion decl = null;
      try{
          decl = GlobalVariables.asignaFuncionID(i, t); 
      } catch(ErrorSemantico e) {
          ErrorHandler.reportaError(e);
      }   

			// Mismas consideraciones que en la siguiente produccion
      GlobalVariables.entraBloqueFuncion(decl);

			// Genera lo necesario para poder dirigir las llamadas al procedimiento
			I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, decl.getEtiqueta());
			// Probablemente en este caso no es importante pero el nivel de profundidad
			// asociado al preambulo esta en el bloque de activacion superior
			// debido a que se crea aqui
			I3DUtils.crea(OperacionTresDirecciones.PREAMBULO, decl);

      // Dummy construct to pass the information upwards
      RESULT = new SimboloFuncionDecl(decl, null, null);
  :}  
          | FUNCTION TVOID VID:i
  {:  
      DeclaracionFuncion decl = null;
      try{
          decl = GlobalVariables.asignaFuncionID(i, Tipo.getTipo(Tipo.Void));
      } catch(ErrorSemantico e) {
          ErrorHandler.reportaError(e);
      }
			
			// Probablemente el entra bloque deberia estar en otro lado. Pero por ahora
			// no me pienso preocupar mas. Ahora mismo el problema que podriamos
			// observar es que no deja redeclarar los parametros de las funciones
			// Esto funciona asi en java. Ahora mismo lo dejo tal cual.
      GlobalVariables.entraBloqueFuncion(decl);
   
			// Genera lo necesario para poder dirigir las llamadas al procedimiento
			I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, decl.getEtiqueta());
			I3DUtils.crea(OperacionTresDirecciones.PREAMBULO, decl);

      // Dummy construct to pass the information upwards
      RESULT = new SimboloFuncionDecl(decl, null, null);
  :}; 
    

funcionInvk ::= VID:i PARENIZQ params:p PARENDER			
	{: 
			DeclaracionFuncion funcionInvocada = null;
			DeclaracionFuncion funcionInvocadora = null;
			try{
					ArrayList<TipoObject> paramTypes = new ArrayList<>();
					SimboloParams previous = null;
					SimboloParams param = p;
					while(param != null) {							
							paramTypes.add(param.getTipoSubyacente());
							previous = param;
							param = param.getNextParam();
					}

					if (previous != null) {
							previous.markLastParam();
					}

					funcionInvocada = GlobalVariables.compruebaFuncionID(i, paramTypes);
					TypeCheck.parameterMatch(i,p);

					EntornoFuncion entorno = (EntornoFuncion)GlobalVariables.entornoFuncionActual();
					funcionInvocadora = (DeclaracionFuncion)entorno.getIdentificador();
			}catch(ErrorSemantico e){
					ErrorHandler.reportaError(e);
			}

			Declaracion declRetorno = null;
			if (funcionInvocada.hasRetorno()) {
					declRetorno = GlobalVariables.crearVariableTemporal(funcionInvocada.getTipo());			
					// Generamos la llamada
					I3DUtils.crea(OperacionTresDirecciones.LLAMADA, funcionInvocada, funcionInvocadora, declRetorno);
			} else {
					I3DUtils.crea(OperacionTresDirecciones.LLAMADA, funcionInvocada, funcionInvocadora);
			}

			RESULT = new SimboloFuncionInvk(funcionInvocada, declRetorno, i, p);
	:}
		;
		
args ::= argDecl:a nextArg:n								{: 
																RESULT = new SimboloArgs(a,n,true);
															:}
		| 													{: 
																RESULT = null;
															:}
		;

nextArg ::= COMA argDecl:d nextArg:n						{:
																RESULT = new SimboloArgs(d,n,false);
															:}
		|													{: 
																RESULT = null;
															:}
		;
		
argDecl ::= TVAR:t VID:i arrayDef:a 						{:  try{
																	if(a!=null)
																		GlobalVariables.asignaFuncionArgArray(i,t,a);
																	else
																		GlobalVariables.asignaFuncionArg(i,t);
																}catch(ErrorSemantico e){
																	ErrorHandler.reportaError(e);
																}
																RESULT = new SimboloArgDecl(i,Tipo.getTipoSafe(t),a);
															:}
		;
		
contenido ::= contenido:c expresion:e						{: RESULT = new SimboloContenido(c,e); :}
			| contenido:c condicional:d						{: RESULT = new SimboloContenido(c,d); :}
			| contenido:c bucle:b							{: RESULT = new SimboloContenido(c,b); :}
			| contenido:c TRETURN:r Operacion:o PUNTOCOMA	
			{: 
					try{
							TypeCheck.returnTypeMatch(o);
					} catch(ErrorSemantico e) {
							ErrorHandler.reportaError(e);
					}
					DeclaracionFuncion declaracion = (DeclaracionFuncion) GlobalVariables.entornoActual().getIdentificador();
					I3DUtils.crea(OperacionTresDirecciones.RETORNO, declaracion, o.getDeclaracionResultado());
					RESULT = new SimboloContenido(c,r,o); 
			:}
			| 	{: RESULT = null; :}
			;
		
bucle ::= cond_bucle:cb LLAVEIZQ contenido:o  LLAVEDER 
		{: 
				I3DUtils.crea(OperacionTresDirecciones.GOTO, cb.getEtiquetaStart());
				I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, cb.getEtiquetaFin());
				RESULT = new SimboloBucle(cb, o);
		:};

cond_bucle ::= TWHILE PARENIZQ M1:start Operacion:o PARENDER 
		{:
				try{
						TypeCheck.checkBoolean(o, true);
				} catch(ErrorSemantico e) {
						ErrorHandler.reportaError(e);
				}
				String etFin = GlobalVariables.generarEtiqueta();
				Declaracion falseTemp = GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Boolean), "false");
				// TODO No se como tenemos que generar el falso aquí. Tal vez como una constante global ??
				I3DUtils.crea(OperacionTresDirecciones.EQ, o.getDeclaracionResultado(), falseTemp, etFin);
				RESULT = new SimboloCondicionBucle(o, start.getEtiqueta(), etFin);
		:};

M1 ::= 
		{: 
				String etStart = GlobalVariables.generarEtiqueta();
				I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, etStart);
				RESULT = new SimboloEtiqueta(etStart);
		:};

condicional     ::= cond_main_block:cmb condelse:e
    {: 
				I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, cmb.getEtiqueta());
        RESULT = new SimboloCondicional(cmb, e);
    :};

cond_main_block ::= clausula_cond:cc LLAVEIZQ contenido:c LLAVEDER 
		{:
		     String etFin = GlobalVariables.generarEtiqueta();
				 I3DUtils.crea(OperacionTresDirecciones.GOTO, etFin);
				 I3DUtils.crea(OperacionTresDirecciones.ETIQUETA, cc.getEtiqueta());
				 RESULT = new SimboloCondicionalBloquePrincipal(cc, c, etFin);
		:};

clausula_cond   ::= TIF PARENIZQ Operacion:o PARENDER 
			{:
					try {
						TypeCheck.checkBoolean(o, false);
					} catch(ErrorSemantico e) {
						ErrorHandler.reportaError(e);
					} 
  	      String et = GlobalVariables.generarEtiqueta();
					// TODO Forma hacky de hacer que el valor constante False esté en el codigo.
					// Además el valor false y true deberían estar codificados en algun lado para reusarlo
					// en el codigo
					Declaracion falseTemp = GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Boolean), "false");
					// TODO No se como tenemos que generar el falso aquí. Tal vez como una constante global ??
					I3DUtils.crea(OperacionTresDirecciones.EQ, o.getDeclaracionResultado(), falseTemp, et);
					RESULT = new SimboloClausulaCondicion(o, et);
			:};
			
// No es necesario generar nada adicional aqui por dos motivos:
// 1. Cuando se acaba el else no hace falta ningun salto
// 2. La gestion de la etiqueta para el salto de condicion falsa se hace en un bloque superior
condelse ::= TELSE LLAVEIZQ	contenido:e LLAVEDER	{: RESULT = new SimboloCondElse(e); :}
			|	{: RESULT = null; :}
			;
			
expresion ::= asignacion:a 									{: RESULT = new SimboloExpresion(a); :}
			| declaracion:d 								{: RESULT = new SimboloExpresion(d); :}
			| funcionInvk:f PUNTOCOMA						{: RESULT = new SimboloExpresion(f); :}
			;
		
Operacion ::= Operacion1:o
		{: 
				SimboloOperacion operacion = new SimboloOperacion(o);
				operacion.setDeclaracionResultado(o.getDeclaracionResultado());
				RESULT = operacion; 
				
		:}
		| Operacion:o OPSUMA:s Operacion:q
		{: 
				Declaracion declResultadoSuma = null;
				try {
					TypeCheck.typesMatch(o.getTipoSubyacente(), TipoOperador.getTipoOperador(s));
					TypeCheck.typesMatch(q.getTipoSubyacente(), TipoOperador.getTipoOperador(s));
					TypeCheck.typesMatch(o.getTipoSubyacente(), q.getTipoSubyacente());

					declResultadoSuma =  GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Integer));
					I3DUtils.crea(I3DUtils.getTipoOperacion(s), o.getDeclaracionResultado(), q.getDeclaracionResultado(), declResultadoSuma);
				}catch(ErrorSemantico e){
					ErrorHandler.reportaError(e);
				}

				SimboloOperacion resultado = new SimboloOperacion(o,s,q);
				resultado.setDeclaracionResultado(declResultadoSuma);
				RESULT = resultado; 
		:}
		;
	
Operacion1 ::= Operacion2:o
		{:
				SimboloOperacion operacion = new SimboloOperacion(o);
				operacion.setDeclaracionResultado(o.getDeclaracionResultado());
				RESULT = operacion; 
		:}
		| Operacion:o OPPROD:p Operacion:q
		{: 
				Declaracion declResultadoProd = null;
				try{
					TypeCheck.typesMatch(o.getTipoSubyacente(), TipoOperador.getTipoOperador(p));
					TypeCheck.typesMatch(q.getTipoSubyacente(), TipoOperador.getTipoOperador(p));
					TypeCheck.typesMatch(o.getTipoSubyacente(), q.getTipoSubyacente());

					declResultadoProd =  GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Integer));
					I3DUtils.crea(I3DUtils.getTipoOperacion(p), o.getDeclaracionResultado(), q.getDeclaracionResultado(), declResultadoProd);
				} catch(ErrorSemantico e) {
					ErrorHandler.reportaError(e);
				}

				SimboloOperacion resultado = new SimboloOperacion(o,p,q);
				resultado.setDeclaracionResultado(declResultadoProd);
				RESULT = resultado; 
		:}
		;
		
Operacion2 ::= Operacion3:o
		{: 
				SimboloOperacion operacion = new SimboloOperacion(o);
				operacion.setDeclaracionResultado(o.getDeclaracionResultado());
				RESULT = operacion; 
		:}
		| Operacion:o COMPARADOR:c Operacion:q 
		{: 
				Declaracion declResultado = null;
				try{
					TypeCheck.typesMatch(o.getTipoSubyacente(), TipoOperador.getTipoOperador(c));
					TypeCheck.typesMatch(q.getTipoSubyacente(), TipoOperador.getTipoOperador(c));
					TypeCheck.typesMatch(o.getTipoSubyacente(), q.getTipoSubyacente());

					declResultado =  GlobalVariables.crearVariableTemporal(Tipo.getTipo(Tipo.Boolean));
					I3DUtils.crea(I3DUtils.getTipoOperacion(c), o.getDeclaracionResultado(), q.getDeclaracionResultado(), declResultado);
				} catch(ErrorSemantico e) {
					ErrorHandler.reportaError(e);
				}
				SimboloOperacion resultado = new SimboloOperacion(o,c,q);
				resultado.setDeclaracionResultado(declResultado);
				RESULT = resultado; 
		:}
		;
		
Operacion3 ::= factor:f 
		{:
				SimboloOperacion operacion = new SimboloOperacion(f);
				operacion.setDeclaracionResultado(f.getDeclaracionResultado());
				RESULT = operacion; 
		:}
		| Operacion:o OPLOGICO:l Operacion:q 
		{: 
				Declaracion declResultado = null;
				try{
					TypeCheck.typesMatch(o.getTipoSubyacente(), TipoOperador.getTipoOperador(l));
					TypeCheck.typesMatch(q.getTipoSubyacente(), TipoOperador.getTipoOperador(l));
					TypeCheck.typesMatch(o.getTipoSubyacente(), q.getTipoSubyacente());

					declResultado =  GlobalVariables.crearVariableTemporal(Tipo.getTipo(Tipo.Boolean));
					I3DUtils.crea(I3DUtils.getTipoOperacion(l), o.getDeclaracionResultado(), q.getDeclaracionResultado(), declResultado);
				} catch(ErrorSemantico e) {
					ErrorHandler.reportaError(e);
				}
				SimboloOperacion resultado = new SimboloOperacion(o,l,q);
				resultado.setDeclaracionResultado(declResultado);
				RESULT = resultado; 
		:}
		;
	
factor ::= VID:i arrayAsig:a                  
    {:
        Declaracion decl = null;
        try{
            decl = GlobalVariables.compruebaID(i);
						boolean isDeclaracionArray = decl instanceof DeclaracionArray;

						if (a != null && !isDeclaracionArray) {
								// Estamos intentando dereferenciar una variables simple!
								throw new ErrorSemantico("Se ha intentado dereferenciar la variable simple: " + i);
						}


						if (!isDeclaracionArray) {
								// Esto ya es una variable o una constante declarada en nuestra tabla de simbolos
								// asi que no es necesario crear una variable tempral
								RESULT = new SimboloFactor(decl);
						} else if (a == null) {
								// TODO Esto no esta implementado. ¿Como debemos gestionarlo?
								// Si usaramos memoria dinamica ( heap ) esto seria tan sencillo
								// como asignar un valor a otro.
								RESULT = new SimboloFactor(decl);
						} else {
								DeclaracionArray declArray = (DeclaracionArray) decl;
								Declaracion variable = GlobalVariables.crearVariableTemporal(declArray.getTipoDato());
								DeclaracionConstante indice = GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Integer), a.getNumero());
								// Estamos accediendo a un array
								I3DUtils.crea(OperacionTresDirecciones.CARGAR_INDIRECCION, declArray, indice, variable);
								RESULT = new SimboloFactor(declArray, a.getNumero(), variable);
						}
				}catch(ErrorSemantico e){
						ErrorHandler.reportaError(e);
				}
				// TODO Aqui se ha quitado el parametro. Probablemente se debería
				// pasar para pintar todo como toca en el arbol.
    :}
    | VNUMERO:n                     
    {:
			 	DeclaracionConstante decl =  GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Integer), Integer.parseInt(n));
				Declaracion varDecl = GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Integer));
				I3DUtils.crea(OperacionTresDirecciones.COPIA, decl, varDecl);
				// TODO Aqui se ha quitado el parametro que pasaba el número. Probablemente se debería
				// pasar para pintar todo como toca en el arbol.
        RESULT = new SimboloFactor(varDecl);
    :}
    | VBOOLEANO:b                    
    {: 
			 	DeclaracionConstante decl =  GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Boolean), b);
				Declaracion varDecl = GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.Boolean));
				I3DUtils.crea(OperacionTresDirecciones.COPIA, decl, varDecl);				
				// TODO Aqui se ha quitado el parametro. Probablemente se debería
				// pasar para pintar todo como toca en el arbol.
        RESULT = new SimboloFactor(varDecl);
    :}
    | VSTRING:s                      
    {: 
    			OverflowCheck.string(s);
			 	DeclaracionConstante decl =  GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.String), s);
				Declaracion varDecl = GlobalVariables.crearVariableTemporal(Tipo.getTipoSafe(Tipo.String));
				I3DUtils.crea(OperacionTresDirecciones.COPIA, decl, varDecl);				
				// TODO Aqui se ha quitado el parametro. Probablemente se debería
				// pasar para pintar todo como toca en el arbol.
        RESULT = new SimboloFactor(varDecl); 
    :}
    | funcionInvk:f                    
    {: 
				// En este punto solo tenemos que propagar la declaración del retorno para poder tener en cuenta la variable
				// en otras partes del codigo
        RESULT = new SimboloFactor(f.getDeclaracion()); 
    :}
    | PARENIZQ Operacion:o PARENDER           
    {: 
        RESULT = new SimboloFactor(o.getDeclaracionResultado()); 
		:}
		;
		
params ::= Operacion:o nextParam:n							
		{: 
				// Generamos las instrucciones para empilar cada uno de los parametros.
				Param param;
				Declaracion variable = o.getDeclaracionResultado();
				if (GlobalVariables.isComplexParam(variable)) {
					param = (Param)I3DUtils.crea(OperacionTresDirecciones.COMPLEX_PARAM, o.getDeclaracionResultado());
				} else {
					param = (Param)I3DUtils.crea(OperacionTresDirecciones.PARAM, o.getDeclaracionResultado());
				}
				RESULT = new SimboloParams(o,n,true, param); 
		:}
		| {: RESULT = null; :};

nextParam ::= COMA Operacion:o nextParam:n
		{: 
				Param param;
				Declaracion variable = o.getDeclaracionResultado();
				if (GlobalVariables.isComplexParam(variable)) {
					param = (Param)I3DUtils.crea(OperacionTresDirecciones.COMPLEX_PARAM, o.getDeclaracionResultado());
				} else {
					param = (Param)I3DUtils.crea(OperacionTresDirecciones.PARAM, o.getDeclaracionResultado());
				}
				RESULT = new SimboloParams(o,n,false, param); 
		:}
		| {: RESULT = null; :}
			;
